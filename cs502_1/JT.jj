options {
  JAVA_UNICODE_ESCAPE = true;
}

PARSER_BEGIN(JTLang)
  public class JTLang {}
PARSER_END(JTLang)


SKIP : /* WHITE SPACE */
{
  " "
| "\t"
| "\n"
| "\r"
| "\f"
}

SPECIAL_TOKEN : /* COMMENTS */
{
  <SINGLE_LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <FORMAL_COMMENT: "/**" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
| <MULTI_LINE_COMMENT: "/*" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
}

TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LSQPAREN: "[" >
| < RSQPAREN: "]" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < SEMICOLON: ";" >
| < DOT: "." >
| < ASSIGN: "=" >
| < NE: "!=" >
| < PLUS: "+" >
| < AND : "&&" >
| < CLASS: "class" >
| < EXTENDS: "extends" >
| < ELSE: "else" >
| < FALSE: "false" >
| < IF: "if" >
| < WHILE: "while" >
| < INTEGER: "int" >
| < DOUBLE: "double" >
| < BOOLEAN: "boolean" >
| < MAIN: "main" >
| < NEW: "new" >
| < PUBLIC: "public" >
| < RETURN: "return" >
| < STATIC: "static" >
| < STRING: "String" >
| < THIS: "this" >
| < TRUE: "true" >
| < PRINT: "System.out.println" >
| < VOID: "void" >
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL: ( ["1"-"9"] (["0"-"9"])* | "0" ) >
|
  < DOUBLE_LITERAL: ( ["1"-"9"] (["0"-"9"])* | "0" ) <DOT> (["0"-"9"])* >
|
  < ARRAY_LENGTH: <IDENTIFIER> <DOT> "length" >
}

TOKEN : /* IDENTIFIERS */
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
}

/*************************************
 * The JTLang Grammar Starts Here *
 *************************************/

void Goal() :
{}
{

  MainClass()
  ( TypeDeclaration() )*
  <EOF>
}

void MainClass() :
{}
{
  "class" Identifier() ( ExtendsClass() )* "{"
    "public" "static" "void" "main" "(" "String" "[" "]" Identifier() ")"
    "{" PrintStatement() "}"
  "}"
}

void TypeDeclaration() :
{}
{
  ClassDeclaration()
}

void ClassDeclaration() :
{}
{
  "class" Identifier() ( ExtendsClass() )* "{"
                           ( MethodDeclaration() )*
                       "}"
}

void ExtendsClass() :
{}
{
  "extends" Identifier()
}

void VarDeclaration() :
{}
{
  Type() Identifier() ";"
}

void MethodDeclaration() :
{}
{
  "public" Type() Identifier()
  "(" ( FormalParameterList() )? ")"
  "{" ( LOOKAHEAD(2) VarDeclaration() )*
      ( Statement() )* "return" Expression() ";" "}"
}

void FormalParameterList() :
{}
{
  FormalParameter() ( FormalParameterRest() )*
}

void FormalParameter() :
{}
{
  Type() Identifier()
}

void FormalParameterRest() :
{}
{
  "," FormalParameter()
}

void Type() :
{}
{
  LOOKAHEAD(3)
  ArrayType()
|
  IntegerType()
|
  DoubleType()
|
  BooleanType()
|
  Identifier()
}

void ArrayType() :
{}
{
  "int" "[" "]"
}

void IntegerType() :
{}
{
  "int"
}

void DoubleType() :
{}
{
  "double"
}

void BooleanType() :
{}
{
  "boolean"
}

void Statement() :
{}
{
  Block()
|
  LOOKAHEAD(2)
  AssignmentStatement()
|
  LOOKAHEAD(2)
  ArrayAssignmentStatement()
|
  IfStatement()
|
  WhileStatement()
|
  PrintStatement()
}

void Block() :
{}
{
  "{" ( Statement() )* "}"
}

void AssignmentStatement() :
{}
{
  Identifier() "=" Expression() ";"
}

void ArrayAssignmentStatement() :
{}
{
  Identifier() "[" Expression() "]" "=" Expression() ";"
}

void IfStatement() :
{}
{
  // Add if-then stmt as well
  LOOKAHEAD(IfthenElseStatement()) IfthenElseStatement()
  | IfthenStatement()
}
void IfthenStatement() :
{}
{
  "if" "(" Expression() ")" Statement()
}
void IfthenElseStatement() :
{}
{
  "if" "(" Expression() ")" Statement() "else" Statement()
}
void WhileStatement() :
{}
{
  "while" "(" Expression() ")" Statement()
}

void PrintStatement() :
{}
{
  "System.out.println" "(" Expression() ")" ";"
}

void Expression() :
{}
{
  LOOKAHEAD( PrimaryExpression() "||" )
  OrExpression()
|
  LOOKAHEAD( PrimaryExpression() "==" )
  EqExpression()
|
  LOOKAHEAD( PrimaryExpression() "!=" )
  NotEqExpression()
|
  LOOKAHEAD( PrimaryExpression() "&&" )
  AndExpression()
|
  LOOKAHEAD( PrimaryExpression() "+" )
  AddExpression()
|
  LOOKAHEAD( PrimaryExpression() "-" )
  SubExpression()
|
  LOOKAHEAD( PrimaryExpression() "*" )
  MulExpression()
|
  LOOKAHEAD( PrimaryExpression() "/" )
  DivExpression()
|
  LOOKAHEAD( PrimaryExpression() "." Identifier() "(" )
  MessageSend()
|
  PrimaryExpression()
}

void OrExpression() :
{}
{
  PrimaryExpression() "||" PrimaryExpression()
}

void EqExpression() :
{}
{
  PrimaryExpression() "==" PrimaryExpression()
}

void NotEqExpression() :
{}
{
  PrimaryExpression() "!=" PrimaryExpression()
}

void AndExpression() :
{}
{
  PrimaryExpression() "&&" PrimaryExpression()
}

void AddExpression() :
{}
{
  PrimaryExpression() "+" PrimaryExpression()
}

void SubExpression() :
{}
{
  PrimaryExpression() "-" PrimaryExpression()
}

void MulExpression() :
{}
{
  PrimaryExpression() "*" PrimaryExpression()
}

void DivExpression() :
{}
{
  PrimaryExpression() "/" PrimaryExpression()
}

void MessageSend() :
{}
{
  PrimaryExpression() "." Identifier()
  "(" ( ExpressionList() )? ")"
}
void ExpressionList() :
{}
{
  Expression() ( ExpressionRest() )*
}

void ExpressionRest() :
{}
{
  "," Expression()
}

void PrimaryExpression() :
{}
{
  ArrayLengthExpression()
|
  LOOKAHEAD(2)
  ArrayLookupExression()
|
  IntegerLiteral()
|
  DoubleLiteral()
|
  TrueLiteral()
|
  FalseLiteral()
|
  Identifier()
|
  ThisExpression()
|
  LOOKAHEAD(3)
  ArrayAllocationExpression()
|
  AllocationExpression()
|
  NotExpression()
|
  BracketExpression()
}

void IntegerLiteral() :
{}
{
  <INTEGER_LITERAL>
}

void DoubleLiteral() :
{}
{
  <DOUBLE_LITERAL>
}

void TrueLiteral() :
{}
{
  "true"
}

void FalseLiteral() :
{}
{
  "false"
}

void Identifier() :
{}
{
  <IDENTIFIER>
}

void ThisExpression() :
{}
{
  "this"
}

void ArrayAllocationExpression() :
{}
{
  "new" "int" "[" Expression() "]"
}

void ArrayLengthExpression() :
{}
{
  <ARRAY_LENGTH>
}

void ArrayLookupExression() :
{}
{
  <IDENTIFIER> "[" Expression() "]"
}

void AllocationExpression() :
{}
{
  "new" Identifier() "(" ")"
}

void NotExpression() :
{}
{
  "!" Expression()
}

void BracketExpression() :
{}
{
  "(" Expression() ")"
}

void IdentifierList() :
{}
{
  Identifier() ( IdentifierRest() )*
}

void IdentifierRest() :
{}
{
  "," Identifier()
}

